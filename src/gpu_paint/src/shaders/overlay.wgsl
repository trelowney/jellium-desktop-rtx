// Full-screen blit. Three-vertex triangle covers the viewport with UVs
// in [0,1]; fragment samples the OSD texture at nearest. No scaling —
// CEF always rasterizes at the swapchain's device pixel size, so 1:1
// sampling is both correct and forbids accidental stretching during
// resize.

struct VertexOut {
    @builtin(position) pos: vec4<f32>,
    @location(0) uv: vec2<f32>,
};

@vertex
fn vs_main(@builtin(vertex_index) idx: u32) -> VertexOut {
    var out: VertexOut;
    let x = f32((idx << 1u) & 2u);
    let y = f32(idx & 2u);
    out.uv = vec2<f32>(x, y);
    out.pos = vec4<f32>(x * 2.0 - 1.0, 1.0 - y * 2.0, 0.0, 1.0);
    return out;
}

@group(0) @binding(0) var tex: texture_2d<f32>;
@group(0) @binding(1) var samp: sampler;

@fragment
fn fs_main(in: VertexOut) -> @location(0) vec4<f32> {
    return textureSample(tex, samp, in.uv);
}

// For targets whose composite-alpha mode is PostMultiplied but whose
// compositor still expects premultiplied pixels (metal offers no
// premultiplied mode; CoreAnimation composites premultiplied).
@fragment
fn fs_main_premultiplied(in: VertexOut) -> @location(0) vec4<f32> {
    let c = textureSample(tex, samp, in.uv);
    return vec4<f32>(c.rgb * c.a, c.a);
}
